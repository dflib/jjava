plugins {
    id 'java'
    id 'maven-publish'
    id('com.github.hierynomus.license') version '0.16.1'
    id('com.github.jk1.dependency-license-report')
}

import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jk1.license.render.*
import com.github.jk1.license.filter.*
import io.github.spencerpark.gradle.*

group = 'io.github.spencerpark'
version = '1.3.0'

wrapper {
    gradleVersion = '8.5'
    distributionType = Wrapper.DistributionType.ALL
}

// Add the license header to source files
license {
    header = file('LICENSE')
    exclude '**/*.json'
    mapping {
        // Use a regular multiline comment rather than a javadoc comment
        java = 'SLASHSTAR_STYLE'
    }
}
build.dependsOn 'licenseFormat'

// Configures the license report generated for the dependencies.
licenseReport {
    excludeGroups = []
    renderers = [
            // Generate a pretty HTML report that groups dependencies by their license.
            new NewInventoryHtmlReportRenderer('dependencies.html'),
            // TODO make sure ci verifies that all licenses are know to be allowed to redistribute before publishing
            new JsonReportRenderer('dependencies.json')
    ]

    // Group same licenses despite names being slightly different (ex. Apache 2.0 vs Apache version 2)
    filters = [new LicenseBundleNormalizer()]

    configurations = ['compile']
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

configurations {
    shade
    // transitive true to make sure that the dependencies of shade dependencies also get shaded into the jar
    shade.transitive = true
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenLocal()
}

dependencies {
    shade group: 'io.github.spencerpark', name: 'jupyter-jvm-basekernel', version: '2.3.0'

    shade group: 'org.apache.ivy', name: 'ivy', version: '2.5.2'
    //shade group: 'org.apache.maven', name: 'maven-settings-builder', version: '3.6.0'
    shade group: 'org.apache.maven', name: 'maven-model-builder', version: '3.6.0'

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

jar {
    //Include all shaded dependencies in the jar
    from configurations.shade
            .collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes('Main-class': 'io.github.spencerpark.ijava.IJava')
    }

    duplicatesStrategy = 'include'
}

processResources {
    def tokens = [
            'version': project.version,
            'project': project.name
    ]
    inputs.properties(tokens)
    filter ReplaceTokens, tokens: tokens
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

tasks.register('zipKernel', Zip) {
    group = 'distribution'
    description = 'Zips jupyter kernel jar with its installation script.'

    // preprocess kernel.json
    def kernelSpecTemplate = file('buildSrc/src/main/resources/kernel.json')
    from(kernelSpecTemplate) {
        include kernelSpecTemplate.name
        filter { line -> line.replaceAll('@JAR_FILE@', "${project.name}-${project.version}.jar") }
    }
    // copy as is install script and jar
    from(file('buildSrc/src/main/resources/install.py'))
    from(jar.archiveFile)
}

zipKernel.dependsOn 'jar', 'generateLicenseReport'